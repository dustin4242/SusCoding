let program = "++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++."

let array = [0]
for(i = 0, 10)
	push(array, 0)
end
let pointer = 0

let fuckInstructions = "><+-.[]"

let isLooping = 0
let innerLoops = 0
let loopStack = [1]
loopStack = loopStack[:0]

for(i = 0, call(len, program))
	let char = program[i]
	if(isLooping == 1)
		if(char == fuckInstructions[5])
			innerLoops = innerLoops + 1
		end
		if(char == fuckInstructions[6])
			if(innerLoops == 0)
				isLooping = 0
			else
				innerLoops = innerLoops - 1
			end
		end
		continue
	end
	if(char == fuckInstructions[0])
		pointer = pointer + 1
	elif(char == fuckInstructions[1])
		pointer = pointer - 1
	elif(char == fuckInstructions[2])
		array[pointer] = array[pointer] + 1
	elif(char == fuckInstructions[3])
		array[pointer] = array[pointer] - 1
	elif(char == fuckInstructions[4])
		let byte = array[pointer]
		let ascii = call(string, byte)
		print(ascii)
	elif(char == fuckInstructions[5])
		if(array[pointer] == 0)
			isLooping = 1
		else
			push(loopStack, i)
		end
	elif(program[i] == fuckInstructions[6])
		let length = call(len, loopStack)
		if(array[pointer] != 0)
			i = loopStack[length - 1]
		else
			loopStack = loopStack[:length - 1]
		end
	end
end
